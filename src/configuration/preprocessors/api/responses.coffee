possibleResponses = require "./possible-responses"
{cat, isArray} = require "fairmont"

# Gateway does not, by default, support arbitrary HTTP responses.  Each response
# type must be explicitly specified in an API action description.  The code
# below adds these responses.  In addition to the expected response, this
# adds error responses (ex 404, 403) to the API action descriptions.
module.exports = (description) ->

  # Array of possible responses generated by the Lambda integration
  addIntegrationResponses = (a, methodList) ->
    addDefault = (status) ->
      response =
        StatusCode: status
        ResponseParameters:
          "method.response.header.Access-Control-Allow-Headers": "'Content-Type,X-Amz-Date,Authorization,X-Api-Key,X-Amz-Security-Token'"
          "method.response.header.Access-Control-Allow-Methods": "'#{methodList}'"
          "method.response.header.Access-Control-Allow-Origin": "'*'"

      if a.signature.accept?
        response.ResponseParameters["method.response.header.Content-Type"] = "'#{a.signature.accept}'"
      response

    addOthers = (statuses) ->
      out = []
      for code in statuses
        out.push(
          StatusCode: code
          SelectionPattern: possibleResponses[code]
          ResponseParameters:
            "method.response.header.Access-Control-Allow-Headers": "'Content-Type,X-Amz-Date,Authorization,X-Api-Key,X-Amz-Security-Token'"
            "method.response.header.Access-Control-Allow-Methods": "'#{methodList}'"
            "method.response.header.Access-Control-Allow-Origin": "'*'"
        )
      out


    s = a.signature.status
    if !isArray s
      return [addDefault s] # Only one response specified.
    else
      # Start by adding the "default" response, then all others.
      [d, others...] = s
      return cat [addDefault d], (addOthers others)


  # Array of possible responses whitelisted by the Method response, coming from
  # the Integration response.
  addMethodResponses = (a) ->
    addDefault = (status) ->
      response =
        StatusCode: status
        ResponseParameters:
          "method.response.header.Access-Control-Allow-Headers": true
          "method.response.header.Access-Control-Allow-Methods": true
          "method.response.header.Access-Control-Allow-Origin": true

      if a.signature.accept?
        response.ResponseParameters["method.response.header.Content-Type"] = true
      return response

    addOthers = (statuses) ->
      out = []
      for code in statuses
        out.push(
          StatusCode: code
          ResponseParameters:
            "method.response.header.Access-Control-Allow-Headers": true
            "method.response.header.Access-Control-Allow-Methods": true
            "method.response.header.Access-Control-Allow-Origin": true
        )
      out

    s = a.signature.status
    if !isArray s
      return [addDefault s] # Only one response specified.
    else
      # Start by adding the "default" response, then all others.
      [d, others...] = s
      return cat [addDefault d], (addOthers others)




  {resources} = description
  for r, resource of resources
    for a, action of resource.actions
      resources[r]["actions"][a].IntegrationResponses = addIntegrationResponses action, resource.methodList
      resources[r]["actions"][a].MethodResponses = addMethodResponses action

  description.resources = resources
  description
